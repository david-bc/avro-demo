buildscript {
	ext {
		springBootVersion = '1.5.7.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.10.0"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: "com.commercehub.gradle.plugin.avro"


group = 'com.bettercloud.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java','src/generated/java']
		}
	}
}

repositories {
	mavenCentral()
	jcenter()
}


ext {
	springCloudVersion = 'Dalston.SR4'
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')

	compile('org.springframework.cloud:spring-cloud-stream-binder-kafka')
	compile('org.springframework.kafka:spring-kafka')

	compile('org.springframework.cloud:spring-cloud-starter-hystrix')
	compile('org.springframework.retry:spring-retry')

	compile "org.apache.avro:avro:1.8.1"

	compileOnly('org.projectlombok:lombok')

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask) {
	source("src/avro/")
	outputDir = file("src/generated/java")
}

compileJava.source(generateAvro.outputs)
